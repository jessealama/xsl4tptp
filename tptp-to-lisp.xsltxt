stylesheet "1.0";
import "utils/die.xsl";
import "utils/list.xsl";
output method="text";

tpl [/] {
  if [tstp] {
    apply [tstp];
  } else {
    msg terminate="yes" "Error: the required tstp root element is missing.";
  }
}

tpl [tstp] {
  "("; "\n"; apply [formula]; "\n"; ")"; "\n";
}

tpl [formula[not(@name)]] {
  apply [.] (#message = "Error: formulas must have names.") mode="die";
}

tpl [formula[not(@syntax)]] {
  apply [.] (#message = "Error: formulas must have a syntax.") mode="die";
}

tpl [formula[not(@status)]] {
  apply [.] (#message = "Error: formulas must have a status.") mode="die";
}

tpl [formula[@name and @syntax and @status]] {
  $n = `@name`;
  $status = `@status`;
  $syntax = `@syntax`;
  "("; $syntax; " "; $n; " "; $status; " "; apply [*[1]];
  if [source] {
    " "; apply [source];
  }
  if [useful-info] {
    " "; apply [useful-info];
  }
  ")"; "\n";
}

tpl [source[not(non-logical-data) and not(number) and not(string)]] {
  msg terminate="yes" "Error: don't know how to handle a source node that lacks a non-logical-data child, a number child, and a string child.";
}

tpl [source[non-logical-data]] {
  apply [non-logical-data];
}

tpl [source[number]] {
  apply [number];
}

tpl [source[string]] {
  apply [string];
}

tpl [useful-info[not(non-logical-data) and not(number) and not(string)]] {
  msg terminate="yes" "Error: don't know how to handle a useful-info node that lacks a non-logical-data child, a number child, and a string child.";
}

tpl [useful-info[non-logical-data]] {
  apply [non-logical-data];
}

tpl [useful-info[string]] {
  apply [string];
}

tpl [non-logical-data[number]] {
  apply [number];
}

tpl [non-logical-data[@name and *]] {
  "("; `@name`; " "; list (#elems = `*`, #separ = " "); ")";
}

tpl [non-logical-data[@name and not(*)]] {
  `@name`;
}

tpl [non-logical-data[not(@name)]] {
  "("; list (#elems = `*`, #separ = " "); ")";
}

tpl [number] {
  `@name`;
}

tpl [string] {
  `@name`;
}

//////////////////////////////////////////////////////////////////////
// Rendering TPTP formulas
//////////////////////////////////////////////////////////////////////

tpl [disjunction] {
 "(or "; apply [*[1]]; " "; apply [*[2]]; ")";
}

tpl [conjunction] {
 "(and "; apply [*[1]]; " "; apply [*[2]]; ")";
}

tpl [implication] {
 "(implies "; apply [*[1]]; " "; apply [*[2]]; ")";
}

tpl [reverse-implication] {
 "(implies "; apply [*[2]]; " "; apply [*[1]]; ")";
}

tpl [equivalence] {
 "(iff "; apply [*[1]]; " "; apply [*[2]]; ")";
}

tpl [nonequivalence] {
 "(xor "; apply [*[1]]; " "; apply [*[2]]; ")";
}

tpl [predicate[@name and not(@name = "=")]] {
  if [*] {
    "("; `@name`; " "; list (#separ = " ", #elems = `*`); ")";
  } else {
    "("; `@name`; ")";
  }
}

tpl [negation] {
  "(not "; apply [*[1]]; ")";
}

tpl [quantifier[@type = "universal"]] {
  "(all ";
    "("; list (#separ = " ", #elems = `variable`); ")";
    " ";
    apply [*[position() = last()]];
  ")";
}

tpl [quantifier[@type = "existential"]] {
  "(exists ";
    "(";list (#separ = " ", #elems = `variable`); ")";
    " "; apply [*[position() = last()]];
  ")";
}

tpl [defined-predicate[@name = "false"]] {
  "(false)";
}

tpl [defined-predicate[@name = "true"]] {
  "(true)";
}

//////////////////////////////////////////////////////////////////////
// Terms
//////////////////////////////////////////////////////////////////////

tpl [function[@name]] {
  if [*] {
    "("; `@name`; " "; list (#separ = " ", #elems = `*`); ")";
  } else {
    "("; `@name`; ")";
  }
}

tpl [variable[@name]] {
  "?"; `@name`;
}

//////////////////////////////////////////////////////////////////////
// Strings
//////////////////////////////////////////////////////////////////////

tpl [string[@name]] {
  $n = `@name`;
  `concat ("'", $n, "'")`;
}
